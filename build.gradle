plugins {
  id 'java'
  id 'idea'
  id 'eclipse'
  id 'net.minecrell.licenser' version '0.3'
}

group = 'com.neolumia'
version = '1.1.1'
description = 'Automatic inventory sorting'

ext {
  url = 'https://neolumia.com/'
  bukkitVersion = '1.12.2-R0.1-SNAPSHOT'
  materialVersion = '1.0.15'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
  mavenCentral()
  maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
  maven { url 'https://neolumia-187016.appspot.com/' }
}

dependencies {
  compileOnly group: 'org.spigotmc', name: 'spigot-api', version: bukkitVersion
  testCompile group: 'org.spigotmc', name: 'spigot-api', version: bukkitVersion

  compileOnly group: 'com.neolumia', name: 'material-api', version: materialVersion
  testCompile group: 'com.neolumia', name: 'material-api', version: materialVersion
}

task sourceJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourceJar
  archives javadocJar
}

processResources {
  expand(version: project.version, description: project.description)
  from 'LICENSE'
}

license {
  header = file('HEADER.txt')
  include '**/*.java'
  newLine = true
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.5.1'
}

if(project.hasProperty('cleanFiles')) {
  jar.archiveName = 'AutomaticInventory.jar'
}

apply from: 'gradle/publish.gradle'
